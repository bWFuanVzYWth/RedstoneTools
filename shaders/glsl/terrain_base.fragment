#version 300 es

#pragma optimize(on)

precision mediump float;

#include "shaderfunction.lin"

uniform sampler2D TEXTURE_0;
uniform sampler2D TEXTURE_1;

centroid in vec2 uv0;
in vec4 lig;
#if FAST_FOG == 1
in vec4 fogcol;
#endif
#if REDSTONE_POWER
in vec4 col;
#endif
#if REDSTONE_POWER || CHUNK_BOUND
in vec4 chupos;
#endif
#if LIGHT_MAP
in vec2 uv1;
flat in vec2 flat_uv1;
#endif

out vec4 FragColor;

void main() {
#ifdef BYPASS_PIXEL_SHADER
	FragColor = vec4(0.0, 0.0, 0.0, 0.0);
	return;
#else

	vec4 texcol = texture(TEXTURE_0, uv0);

#if defined(ALPHA_TEST) && !defined(SEASONS_FAR)
#ifdef ALPHA_TO_COVERAGE
#define ALPHA_THRESHOLD 0.05
#else
#define ALPHA_THRESHOLD 0.50
#endif
	if(texcol.a < ALPHA_THRESHOLD)
		discard;
#endif

	vec4 outcol = texcol * lig;

#if REDSTONE_POWER || CHUNK_BOUND || LIGHT_MAP
	vec3 blopos = fract(chupos.xyz);
#endif

#if CHUNK_BOUND || LIGHT_MAP
	vec4 ddxpos = dFdx(chupos);
	vec4 ddypos = dFdy(chupos);
	vec3 N = normalize(cross(ddxpos.xyz, ddypos.xyz));
#endif

#if defined(ALPHA_TEST) && !defined(SEASONS) && !defined(SEASONS_FAR) && !defined(BLEND)
#if REDSTONE_POWER
	outcol.rgb = render_redstone_power(outcol.rgb, col.rgb, blopos.xyz, TEXTURE_0);
#endif
#endif

#if CHUNK_BOUND
	outcol.rgb = render_chunk_bound(outcol.rgb, chupos.xyz, N, blopos.xyz);
#endif

#if LIGHT_MAP == 1
	outcol.rgb = render_light_bound(outcol.rgb, uv1, N, blopos);
#endif

#if defined(FANCY) && FAST_FOG == 1
	outcol.rgb = mix(outcol.rgb, fogcol.rgb, fogcol.a);
#endif

	FragColor = outcol;

#endif
}
